#Script that exports PF contingency analysis results for each separate hour. Run contingency analysis with multiple defines dates/times beforehand. Works for DC contingency, for AC contingency change the results location (Contingency Analysis DC.ElmRes -> Contingency Analysis AC.ElmRes)
#Variable string | save_loc | C:\\....\file.xlsx | Save location

!Variable definition
object oTable;
set sReports;
int iCount;
string strPath;
int iReset;
object oContingencyAnalysis;
object oContingencyReport;
object case,
       elmRes;
set resObjs;

object oDCContRes;
set setDCContSubRes;

object prj,zone,obj;
string prjName;

int tstart, tstop, telapsed;
tstart = GetSystemTimeUTC();

! Get current project and name, write out into the console
prj = GetActiveProject();
Info('Izvoz rezultatov za projekt %s', prjName);
! Get contingency report object 
oContingencyReport = GetFromStudyCase('ComCntreport');

! Get DC contingency results and subresults for each hour
oDCContRes = GetFromStudyCase('Contingency Analysis DC.ElmRes');
setDCContSubRes = oDCContRes.GetContents('*.ElmRes');
! Loop through result files 
for(elmRes=setDCContSubRes.First();elmRes;elmRes=setDCContSubRes.Next()){
  Info('Izbrana datoteka z rezultati %s',elmRes);
  !Select current result file in contingency report dialog window. Enable limits and set loading limit to 0 - printing all results
  oContingencyReport:p_rescnt = elmRes;
  oContingencyReport:iopt_ml = 1;
  oContingencyReport:iopt_maxl = 0;
  !Generate contingency report
  oContingencyReport.Execute();
  !Create save path based on save location, project name and result file name (datetime)
  strPath=sprintf('%s%s%s%s%s',save_loc,prj:loc_name,' ',elmRes:loc_name,'.xlsx');
  !Get report
  sReports=GetTableReports();
  iCount=sReports.Count();
  !Ce je report ga izvozi v excel
  if(iCount>=1){
    for(oTable=sReports.First();oTable;oTable=sReports.Next()){
      oTable.ExportToExcel(strPath,0); ! 1 opens the file directly
      Info('Rezultati shranjeni v %s',strPath);
    }
  }
  else{
    Info('Napaka pri shranjevanju rezultatov');
  }
  CloseTableReports();
}

! Calculate and print elapsed time to console
tstop = GetSystemTimeUTC();
telapsed = tstop-tstart;
Info('Skripta izvedena v casu %d sekund', telapsed);
